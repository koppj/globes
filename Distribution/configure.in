dnl GLoBES -- General LOng Baseline Experiment Simulator
dnl (C) 2002 - 2004,  The GLoBES Team
dnl
dnl GLoBES is mainly intended for academic purposes. Proper
dnl credit must be given if you use GLoBES or parts of it. Please
dnl read the section 'Credit' in the README file.
dnl
dnl This program is free software; you can redistribute it and/or modify
dnl it under the terms of the GNU General Public License as published by
dnl the Free Software Foundation; either version 2 of the License, or
dnl (at your option) any later version.
dnl
dnl This program is distributed in the hope that it will be useful,
dnl but WITHOUT ANY WARRANTY; without even the implied warranty of
dnl MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
dnl GNU General Public License for more details.
dnl
dnl You should have received a copy of the GNU General Public License
dnl along with this program; if not, write to the Free Software
dnl Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307  USA
dnl
dnl Process this file with autoconf to produce a configure script.

AC_PREREQ(2.57)

dnl Versioning of GLoBES
dnl last digit: increment whenever code has changed
dnl middle digit: increment when features have been introduced/removed
dnl first digit: major improvements/rewrites
dnl In contrast to the result of autoupdate I had to swap the
dnl the AC_INIT and AC_CONFIG_SRCDIR
AC_INIT([globes],[2.1.0])
AC_CONFIG_SRCDIR([source/glb_wrapper.c])

dnl Tellin autoconf we are going to use automake
AM_INIT_AUTOMAKE
AC_CONFIG_HEADERS([config.h:config-h.in])
dnl To avoid problems due to clock skew and/or lacking autotools on
dnl user system
AM_MAINTAINER_MODE

dnl Versioning of libglobes.so
dnl code changed: c:r+1:a
dnl API changed: c+1:0:a
dnl -> API extended: c:0:a+1
dnl -> API shrunken: c:0:0
GLBLIBVERSION="5:0:2"

dnl From here no editing !
AC_SUBST(GLBLIBVERSION)
AC_DEFINE_UNQUOTED(GLB_LIBVERSION,"$GLBLIBVERSION",[The version of libglb in libtool format c:r:a])

dnl This part serves to convert Makefile.in in examples to a
dnl Makefile which works automagically 

dnl list of standard targets (the space before the closing quotes is important)
allowed="/usr /usr/local "

dnl assume there is a prefix which is not in `allowed'
GLBLDR="${prefix}/lib"
GLBLDF="${GLBLDR}/libglobes.so -Wl,--rpath -Wl,${GLBLDR} ${GLBLDR}/globes/libglb.so -Wl,--rpath -Wl,${GLBLDR}/globes"
GLBMLDF="${GLBLDR}/globes/libglb.so -Wl,--rpath -Wl,${GLBLDR}/globes"
GLBINC="-I${prefix}/include/"
standard="no"

dnl no prefix given
if test $prefix = NONE; then
  GLBLDF="-lglobes -lglb"
  GLBMLDF="-lglb"
  GLBINC=""
  standard="yes"	
fi

dnl prefix is one of the standard targets where only root can install to
for allow in ´echo $allowed´
 do
	if test $prefix = $allow; then
  		GLBLDF="-lglobes -lglb"
 		GLBMLDF="-lglb"
 		GLBINC=""
 		standard="yes"	
	fi
 done

dnl Output our knowledge to the Makefiles
AC_SUBST(GLBMLDF)
AC_SUBST(GLBLDF)
AC_SUBST(GLBINC)

dnl Keeping track of modules which should be preloaded
dnl if necessary
GLBMODULES="glb_prior_module.la"
AC_SUBST(GLBMODULES)

dnl Checks for programs.
AC_PROG_AWK
AC_PROG_CC
AC_PROG_CXX
AC_PROG_CPP
AC_PROG_INSTALL
AC_PROG_LN_S
AC_PROG_YACC
AC_PROG_LEX

dnl Checks wether libtool exists on this machine
dnl in the user path (needed for globes-module to work properly)
AC_CHECK_PROG([HAVE_LIBTOOL],[libtool],yes,no)

dnl Trying to get support for libtdl
dnl the following lines are taken from the libtool manual
dnl Enable building of the convenience library
dnl and set LIBLTDL accordingly
AC_LIBLTDL_CONVENIENCE(libltdl)

if test -z "$enable_ltdl_install$enable_ltdl_convenience"; then
  if test -f ${srcdir}/ltmain.sh; then
    # if libltdl is libtoolized, it is assumed to be stand-alone and
    # installed unless the command line overrides it (tested above)
    enable_ltdl_install=yes
  else
    AC_MSG_WARN([*** The top-level configure must select either])
    AC_MSG_WARN([*** [A""C_LIBLTDL_INSTALLABLE] or [A""C_LIBLTDL_CONVENIENCE].])
    AC_MSG_ERROR([*** Maybe you want to --enable-ltdl-install?])
  fi
fi

dnl Check for dlopen support
AC_LIBTOOL_DLOPEN
AC_LIBTOOL_WIN32_DLL
dnl Configure libtool
AC_PROG_LIBTOOL
dnl Configure libltdl
dnl AC_CONFIG_SUBDIRS(libltdl)
AC_C_INLINE
AC_LIB_LTDL([libltdl])
dnl AC_PROG_LIBTOOL
AC_SUBST([LIBTOOL_DEPS])
dnl Substitute LTDLINCL and LIBLTDL in the Makefiles
AC_SUBST(LTDLINCL)
AC_SUBST(LIBLTDL)


dnl Checks for libraries.
AC_CHECK_LIB(m,main,[],AC_MSG_ERROR([libm not found]))
AC_CHECK_LIB(gslcblas,main,[],AC_MSG_ERROR([libgslcblas not found]))
AC_CHECK_LIB(gsl,main,[],AC_MSG_ERROR([libgsl not found]))

dnl Checking wether f2c is there (or something similar, like g2c)
AC_LIBF2C_CONVENIENCE

dnl Handling the BLAS/LAPACK stuff
AC_LAPACK_CONVENIENCE

dnl Checks for header files.
AC_HEADER_STDC

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_TYPE_SIZE_T

dnl Checks for library functions.
AC_CHECK_FUNCS(strdup)

dnl Experimental rpm-builing support
RPM_RELEASE=0
AC_SUBST(RPM_RELEASE)
dnl
AM_RPM_INIT
dnl   dnl Enable or disable the rpm making rules in Makefile.am
AM_CONDITIONAL(MAKE_RPMS, test x$make_rpms = xtrue)
AC_CONFIG_FILES(globes.spec)

AC_CONFIG_FILES([Makefile libf2c/Makefile lapack/Makefile\
 globes/Makefile source/Makefile\
 examples/Makefile\
 data/Makefile modules/MakefileExample modules/Makefile globes-config.src\
 globes-module.src libltdl/Makefile])

AC_OUTPUT

dnl pretty printing
stars=""
emtpy=""
while [[  ${#stars} != ${#prefix} ]]
do
	stars="${stars}*"
	empty="${empty} "
done

dnl Remind the user of things he might need ;-)
if test $standard = "yes" ; then
 echo "********************************************"
 echo "*          After 'make install':           *"
 echo "* Do not forget to run 'ldconfig' as root! *"
 echo "********************************************"	
else
 echo "***********************${stars}********************" 
 echo "*              After 'make install':   ${empty}   *"
 echo "* Do not forget to add ${prefix}/bin to your path! *"
 echo "***********************${stars}********************"
fi