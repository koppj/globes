%!GLoBES

$version="3.0.0"


/***************************************************************************
 *            G L o B E S   -   M I N I - W O R K S H O P                  *
 *                                                                         *
 *                     May 13, 2010, Fermilab                              *
 *                                                                         *
 *                A sample implementation of LBNE                          *
 ***************************************************************************/


/* ####### Beam flux ####### */

nuflux(#FNAL)<
        @flux_file="dusel120e250i002dr280dz1300km_flux.txt"
        @time = 3       /* years */
        @power = 7.3    /* 10^20 pot/yrs */
        @norm = 1.0544e17
>


/* ####### Detector settings ###### */

$target_mass =         100.0    /* kt (fiducial mass) */


/* ####### Energy window ####### */

$bins =                476
$emin =                 0.5     /* GeV */
$emax =                60.0     /* GeV */

$sampling_points =             476
$sampling_min =                 0.05    /* GeV */
$sampling_max =                60.0     /* GeV */


/* ####### Baseline setting ####### */

$profiletype =          3 
$densitytab =           {2.8} 
$lengthtab =            {1300}   /* km */


/* ####### Technical information ####### */

$filter_state = 0 
$filter_value = 1000000 


/* ####### Energy resolution ####### */

energy(#electron)<
        @type = 1
        @sigma_e = {0,0,0.085}
>

energy(#muon)<
        @type = 1
        @sigma_e = {0,0,0.085}
>


/* ####### Cross sections ####### */

cross(#CC)<
        @cross_file = "XCC.dat"
>

cross(#NC)<
        @cross_file = "XNC.dat"
>

cross(#QE)<
        @cross_file = "XQE.dat"
>


/* ####### Channel definitions ####### */

channel(#nu_mu_CC)<
        @channel =      #FNAL:  +:      m:      m:      #CC:    #muon
>

channel(#nu_mu_QE)<
        @channel =      #FNAL:  +:      m:      m:      #QE:    #muon
>

channel(#nu_mu_NC)<
        @channel =      #FNAL:  +:      NOSC_m: NOSC_m: #NC:    #electron
>

channel(#nu_e_beam)<
        @channel =      #FNAL:  +:      e:      e:      #CC:    #electron
>

channel(#nu_e_signal)<
        @channel =      #FNAL:  +:      m:      e:      #CC:    #electron       
>


/* ####### Setting the rules ####### */
 
rule(#Nu_E_Appearance)<
        @signal = 0.137@#nu_e_signal
        @signalerror = 0.01 : 0.0001
        
        @background =  2.3e-4@#nu_mu_CC : 0.0034@#nu_mu_NC : 0.096@#nu_e_beam
        @backgrounderror = 0.05 : 0.0001
                
        @sys_on_function = "chiSpectrumTilt"
        @sys_off_function = "chiNoSysSpectrum"
>

rule(#Nu_Mu_Disappearance)<
        @signal = 0.973@#nu_mu_QE
        @signalerror = 0.01 : 0.0001
        
        @background =  0.034@#nu_mu_NC
        @backgrounderror = 0.1 : 0.0001
                
        @sys_on_function = "chiSpectrumTilt"
        @sys_off_function = "chiNoSysSpectrum"
>


/**********************END**********************/
